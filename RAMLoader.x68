*LOADS A PROGRAM INTO THE LISA'S RAM OVER SERIAL (57600 BAUD)
SCC EQU $FCD201
MEMADDRESS EQU $1000
TIMEOUT EQU $FFFF
MESSAGE EQU $FE0088
MONITOR EQU $FE0084

    ORG    $800
   
START:
    MOVE.L #SCC,A1 *THE BASE ADDRESS OF THE SCC IN MEMORY
    MOVE.L #INITDATA,A2 *WHERE ALL OF THE INITIALIZATION DATA THAT NEEDS TO BE LOADED INTO THE SCC IS LOCATED
    MOVE.L #$1,D0 *LETS US KNOW IF THIS IS THE FIRST RUN OF THE RECEIVE LOOP (WE SHOULDN'T START THE TIMEOUT TIMER UNTIL THE FIRST BYTE IS RECEIVED)
    MOVE.L #$0,D1 *ZERO OUT THE TIMEOUT COUNTER
INIT:
    MOVE.B (A2)+,(A1) *TRANSFER ALL OF THE INITIALIZATION DATA INTO THE SCC
    CMPI.B #$FF,(A2) *UNTIL WE READ AN FF (MEANING END OF THE INITIALIZATION INFO)
    BNE INIT
    MOVE.L #MEMADDRESS,A4 *PUT THE MEMORY ADDRESS AT WHICH WE WANT TO LOAD THE PROGRAM INTO A4
    MOVE.L #STARTSEND,A3 *LOAD THE POINTER TO A "START SENDING DATA" MESSAGE
    MOVE.L #13,D5
    MOVE.L #24,D6 *SET THE ROW AND COLUMN
    JSR MESSAGE *AND GO DISPLAY IT
RECEIVE:
    CMPI.L #$1,D0
    BEQ FIRSTTIME *IF WE HAVEN'T RECEIVED THE FIRST BYTE YET, WE DON'T WANT TO START THE TIMEOUT COUNTER
    CMPI.L #TIMEOUT,D1
    BEQ DONE *IF WE'VE HIT THE TIMEOUT, THE TRANSFER IS DONE
    ADD.L #$1,D1 *IF NOT, INCREMENT THE TIMEOUT COUNTER
    BTST.B #$0,(A1) *CHECK TO SEE IF WE HAVE ANY DATA IN THE SCC BUFFER
    BEQ RECEIVE *IF NOT, JUST LOOP UNTIL WE DO
    MOVE.L #$0,D1
    MOVE.B (4,A1),(A4)+ *IF SO, MOVE THE BYTE (OFFSET BY 4 FROM THE SCC BASE ADDRESS) INTO THE NEXT MEMORY LOCATION
    JMP RECEIVE *AND WAIT FOR THE NEXT BYTE
FIRSTTIME:
    MOVE.L #$0,D0 *TELLS THE RECEIVE FUNCTION THAT IT ISN'T THE FIRST TIME ANYMORE AND TO THUS ENABLE THE TIMEOUT
    BTST.B #$0,(A1) *CHECK TO SEE IF WE HAVE ANY DATA IN THE SCC BUFFER
    BEQ FIRSTTIME *IF NOT, JUST LOOP UNTIL WE DO
    MOVE.B (4,A1),(A4)+ *IF SO, MOVE THE BYTE (OFFSET BY 4 FROM THE SCC BASE ADDRESS) INTO THE NEXT MEMORY LOCATION
    JMP RECEIVE *NOW GO BACK TO THE REGULAR RECEIVE FUNCTION
DONE:
    MOVE.L #FINISHED,A3 *IF SO, LOAD THE POINTER TO A "FINISHED!" MESSAGE
    MOVE.L #14,D5
    MOVE.L #24,D6 *SET THE ROW AND COLUMN
    JSR MESSAGE *AND DISPLAY IT TO THE USER
    JMP MEMADDRESS *THEN JUMP TO THE START OF THE LOADED PROGRAM
    
INITDATA:
    DC.B $9,$C0,$B,$D0,$4,$44,$E,$1,$3,$C1,$5,$E8,$C,$0,$D,$0,$FF *THE SCC INITIALIZATION DATA (IN THE FORMAT [REGISTER NUMBER],[DATA]) WITH AN FF AT THE END TO TELL THE CODE WHEN WE'RE DONE
STARTSEND:
    DC.B 'START TRANSFER WHEN READY...',0 *STATUS MESSAGES 
FINISHED:
    DC.B 'DONE - JUMPING TO PROGRAM...',0
    END FINISHED   

*~Font name~Courier New~
*~Font size~10~
*~Tab type~1~
*~Tab size~4~
